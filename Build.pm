use v6;

use CSS::Specification::Build;
use Panda::Builder;
use Panda::Common;
use CSS::Module::MetaData;

class Build is Panda::Builder {

    method build($where) {

        indir $where, {

            my %props = $CSS::Module::MetaData::property.list;
            my $level = 3.0;

            for (<etc css3x-background-20120724.txt> => <CSS3 Backgrounds_and_Borders>,
                ) {
                my ($input-spec, $class-isa) = .kv;
                my $input-path = $*SPEC.catfile( |@$input-spec );

                for :interface<Interface>,
                    :actions<Actions>,
                    :grammar<Grammar> {
                    my ($type, $subclass) = .kv;
                    my $name = (<CSS Module>, @$class-isa, <Spec>,  $subclass).flat.join('::');

                    my $class-dir = $*SPEC.catdir(<lib CSS Module>, @$class-isa, <Spec>);
                    mkdir $class-dir;

                    my $class-path = $*SPEC.catfile($class-dir, $subclass ~ '.pm');

                    say "Building $input-path => $name";
                    temp $*IN  = open $input-path, :r;
                    temp $*OUT = open $class-path, :w;

                    CSS::Specification::Build::generate( $type, $name );
                }

                my @summary = CSS::Specification::Build::summary( :$input-path );
                for @summary {
                    my %detail = %$_;
                    my $prop-name = %detail<name>:delete;
                    %props{$prop-name}{.key} = .value
                        for %detail.pairs;
                    %props{$prop-name}<level> //= $level;
                }

                my $class-dir = $*SPEC.catdir(<lib CSS Drafts>);
                my $class-path = $*SPEC.catfile( $class-dir, 'MetaData.pm' );
                my $class-name = 'CSS::Drafts::MetaData';
                say "Building $class-name";
                {
                    my $*OUT = open $class-path, :w;
                    say 'use v6;';
                    say "#  -- DO NOT EDIT --";
                    say "# generated by: $*PROGRAM_NAME {@*ARGS}";
                    say '';
                    say "module $class-name \{";
                    say "    our \$property = {%props.item.perl};";
                    say '}';
                }
            }
        }
    }
}

# Build.pm can also be run standalone 
sub MAIN(Str $working-directory = '.') {
    Build.new.build($working-directory);
}
